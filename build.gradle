allprojects {
    // Force dependency versions inside of Gradle build scripts themselves. You can see
    // the dependency tree for build scripts with this command: ./gradlew subproject:buildEnvironment
    buildscript {
        configurations.all {
            resolutionStrategy {
                // commons-compress a transitive dependency of the Gradle spring boot plugin. If that is updated,
                // review whether we can remove this.
                force "org.apache.commons:commons-compress:${commonsCompressVersion}"
            }
        }
    }

    // Force dependency versions for regular project dependencies. You can see
    // the dependency tree for regular dependencies with this command: ./gradlew subproject:dependencies
    configurations.all {
        resolutionStrategy {
        }
    }

    tasks.register("printAllProjectDependencies", DependencyReportTask) {
        description 'Prints Out all project dependencies, but does not print out Gradle buildscript dependencies.'
        group 'Dependency Management'
    }

    tasks.register("printAllBuildscriptDependencies", DependencyReportTask) {
        description 'Prints out all dependencies that Gradle uses while building the project, but not the project dependencies themselves.'
        group 'Dependency Management'
        configurations = [buildscript.configurations.classpath]
    }
}

/**
 * When we upgrade the Gradle wrapper, make sure to keep the timeout at 5 minutes.
 */
 tasks.named('wrapper') {
    networkTimeout = 300000
 }
